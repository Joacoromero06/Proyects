%option noyywrap nodefault yylineno
%{
#include "ast.h"
#include "tree.tab.h"
%}

EXP ([eE][+-]?[0-9]+)

%%

"+" |
"-" |
"*" |
"/" |
"|" |
"(" |
")" |
"," |
";" |
"=" {return yytext[0];}

"<"  {yylval.fn = 1; return CMP;} 
">"  {yylval.fn = 2; return CMP;}
"==" {yylval.fn = 3; return CMP;}
"!=" {yylval.fn = 4; return CMP;}
"<=" {yylval.fn = 5; return CMP;}
">=" {yylval.fn = 6; return CMP;}

"sqrt"  {yylval.fn = F_sqrt; return FUNC;} 
"exp"   {yylval.fn = F_exp; return FUNC;}
"log"   {yylval.fn = F_log; return FUNC;}
"print" {yylval.fn = F_print; return FUNC;}

"if" {return IF;}
"else" {return ELSE;}
"then" {return THEN;}
"while" {return WHILE;}
"do" {return DO;}
"let" {return LET;}

[a-zA-Z][a-zA-Z0-9]*    {yylval.s = lookup(yytext);return ID;}
[0-9]+"."?[0-9]*{EXP}? |
[0-9]*"."?[0-9]+{EXP}?  {yylval.d = atof(yytext);return NUMBER;}

"//".*  {} 

\\\n    {printf("$$> ");}
\n      {return EOL;} 
[ \t]   
.       {printf("Caracter misterioso: '%c'", yytext[0]);}
%%



 